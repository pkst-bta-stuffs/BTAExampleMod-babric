plugins {
    id 'babric-loom' version '0.12-SNAPSPOT'
    id 'maven-publish'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
    gluedMinecraftJar()
    remapArchives = false  // Disables remapping since BTA is distributed without obfuscation
    customMinecraftManifest.set("https://babric.github.io/manifest-polyfill/b1.7.3.json")
}

repositories {
    maven {
        name = 'Babric'
        url = 'https://maven.glass-launcher.net/babric'
    }
    ivy {
        url = "https://github.com/Better-than-Adventure"
        patternLayout {
            artifact("[organisation]/releases/download/v[revision]/[module].jar")
            m2compatible = true
        }
        metadataSources { artifact() }
    }
    maven {
        name = 'JitPack'
        url = 'https://jitpack.io'
    }
}

dependencies {
    minecraft("com.mojang:minecraft:b1.7.3") // Only used as a dummy, DO NOT use ANY classes from here!
    mappings "babric:barn:b1.7.3+build.5" // Only used as a dummy

    implementation "bta-download-repo:bta:${project.bta_version}" // Our actual Minecraft dependency - BTA

    modImplementation "babric:fabric-loader:${project.loader_version}"

    implementation "org.slf4j:slf4j-api:1.8.0-beta4"
    implementation "org.apache.logging.log4j:log4j-slf4j18-impl:2.16.0"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

tasks.withType(JavaCompile) {
    options.release.set 8
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}

// Disabling development client and server run tasks since they are incompatible
tasks.runClient.enabled = false
tasks.runServer.enabled = false
